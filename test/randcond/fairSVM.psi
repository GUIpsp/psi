// skipped

def popModel(){
    gender := categorical([0.3307,0.6693]);
    if gender == 0 {
        capital_gain := gauss(568.4105, 24248365.5428);
        if capital_gain < 7298.0000 {
            age := gauss(38.4208, 184.9151);
            capital_loss := gauss(86.5949, 157731.9553);
        } else {
            age := gauss(38.8125, 193.4918);
            capital_loss := gauss(117.8083, 252612.0300);
        }
    } else {
        capital_gain := gauss(1329.3700, 69327473.1006);
        if capital_gain < 5178.0000 {
            age := gauss(38.6361, 187.2435);
            capital_loss := gauss(87.0152, 161032.4157);
        } else {
            age := gauss(38.2668, 187.2747);
            capital_loss := gauss(101.7672, 189798.1926);
        }
    }
	return (gender,age,capital_gain,capital_loss);
}

def SVMPrior(){
	params := (-0.0003,0.0006,-5.7363,-0.0002,1.0003);
	rparams := (params[0]+gauss(0,1),params[1]+gauss(0,1),params[2]+gauss(0,1),params[3]+gauss(0,1),params[4]+gauss(0,1));
	def SVM(gender,age,capital_gain,capital_loss){
		N_age := (age - 17.0) / 62.0;
		N_capital_gain := (capital_gain - 0.0) / 22040.0;
		N_capital_loss := (capital_loss - 0.0) / 1258.0;
		t := rparams[0] * gender + rparams[1] * N_age + rparams[2] * N_capital_gain + rparams[3] * N_capital_loss + rparams[4];
		return t;
	}
	return (rparams,SVM);
}

def fair(SVM: ℕ×ℝ×ℝ×ℝ→ℝ):𝔹{
	pr_f := expectation(infer((){
		(gender,age,capital_gain,capital_loss):=popModel();
		observe(gender==0 && age > 18);
		return SVM(gender,age,capital_gain,capital_loss)<0;
	}));
	pr_m := expectation(infer((){
		(gender,age,capital_gain,capital_loss):=popModel();
		observe(gender==1 && age > 18);
		return SVM(gender,age,capital_gain,capital_loss)<0;		
	}));
	return pr_f>0.85*pr_m;
}

def main(){
	(rparams,SVM):=SVMPrior();
	observe(fair(SVM));
	return (rparams);
}
